PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ui: <http://www.w3.org/ns/ui#>
PREFIX pmdcat: <http://publishmydata.com/pmdcat#>

CONSTRUCT {
    ?code
        a skos:Concept;
        rdfs:label ?label;
        skos:inScheme ?scheme;
        skos:notation ?notation;
        skos:broader ?broader;
	skos:narrower ?narrower;
        ui:sortPriority ?sort_priority;
	<http://example.net/def/used> ?used;
    .
} WHERE {
    ?code
        a skos:Concept;
        rdfs:label ?label_;
        skos:inScheme ?scheme;
        .

    BIND(STR(?label_) AS ?label) # strip language tag

    OPTIONAL {
        ?code skos:notation ?notation;
    }

    OPTIONAL {
        ?code ui:sortPriority ?sort_priority;
    }

    OPTIONAL {
        ?code skos:broader ?broader;
    }

    OPTIONAL {
        ?code skos:narrower ?narrower;
    }

    BIND (EXISTS {
      ?dimension qb:codeList ?scheme .

      ?obs ?dimension ?code .
    } AS ?used)
}